
aspect State {

	public State Method.createState() {
		int index = 0;	
		Set<Integer> indexes = new HashSet<Integer>();
		
		if ( !getIsStatic() ) 
			indexes.add(index++);
			
		for ( org.apache.bcel.generic.Type argType : getArgTypes() ) {
			if ( argType instanceof org.apache.bcel.generic.ReferenceType ) 
				indexes.add(index);
			index++;	
		}	
		return new State(indexes, getMaxLocals());
	}	

	coll Set<State> Instruction.statesIn() circular [new HashSet<State>()] with addAll root Method;
	Instruction contributes statesOut() to Instruction.statesIn() for each succ();
	
	syn Set<State> EntryPoint.statesIn() = Collections.singleton(method().createState());

	syn Set<State> Instruction.statesOut() circular [new HashSet<State>()]; 
      	
	eq Instruction.statesOut() =
		States.processStates(statesIn(), new StateProcessor() {
			@Override public State process(State original) {
				return new State(
						original.getLocalVars(), 
						( getProduceStack() > getConsumeStack() ) ? 
							original.getOpStack().push(DontCareSlot.NORMAL_SLOT, getProduceStack() - getConsumeStack()) :
							original.getOpStack().pop(getConsumeStack() - getProduceStack()), 
						original.getCg());
			}
		});
		
	eq ArrayLength.statesOut() =
		States.processStates(statesIn(), new StateProcessor() {
			@Override public State process(State original) {
				return new State(
					original.getLocalVars(), 
					original.getOpStack().pop().push(DontCareSlot.NORMAL_SLOT), 
					original.getCg()); 
			}
		});
	
	eq Athrow.statesOut() =
		States.processStates(statesIn(), new StateProcessor() {
			@Override public State process(State original) {
				return new State(
					original.getLocalVars(), 
					original.getOpStack().pop(getConsumeStack()).push(original.getOpStack().peek()), 
					original.getCg()); 
			}
		});		
	
	
	eq AconstNull.statesOut() =
		States.processStates(statesIn(), new StateProcessor() {
			@Override public State process(State original) {
				ConnectionGraph cg = original.getCg();
				ObjectNode obj = InternalObject.getNullObject();
				ReferenceNode ref = new ReferenceNode(getPosition(), Category.LOCAL);
				cg = cg.addReferenceAndTarget(ref, obj); 
				return new State(
					original.getLocalVars(), 
					original.getOpStack().pop(getConsumeStack()).push(ref), 
					cg); 
			}
		});
	
	syn ReferenceNode FieldAssignmentInstruction.getStoreObjectRef(State frame) = null;
	
	syn ReferenceNode PutField.getStoreObjectRef(State frame) =
		(ReferenceNode) frame.getOpStack().get(frame.getOpStack().size() -2);

	syn ReferenceNode Aastore.getStoreObjectRef(State frame) =
		(ReferenceNode) frame.getOpStack().get(frame.getOpStack().size() -3);
	
	syn ReferenceNode PutStatic.getStoreObjectRef(State frame) = ReferenceNode.getGlobalRef();
				
	eq FieldAssignmentInstruction.statesOut() {
		// notation: obj.field = value
		return States.processStates(statesIn(), new StateProcessor() {
			@Override public State process(State original) {
				OpStack opStack = original.getOpStack();
				ConnectionGraph cg = original.getCg();
				
				// inspect type of value 
				Slot value = opStack.peek();
				
				if ( value instanceof ReferenceNode ) {
					Set<ObjectNode> referredValues = cg.dereference((ReferenceNode) value);
					
					for ( ObjectNode obj : cg.dereference(getStoreObjectRef(original)) ) 
						for ( ObjectNode referredValue : referredValues ) 
							cg = cg.addField(obj, getFieldName(), referredValue);
				}
				
				return new State(
					original.getLocalVars(), 
					opStack.pop(getConsumeStack()), 
					cg);
			}
		});	
	}
	
	
	syn ReferenceNode FieldLoadInstruction.getLoadObjectRef(State frame) = null;
	
	syn ReferenceNode GetField.getLoadObjectRef(State frame) =
		(ReferenceNode) frame.getOpStack().get(frame.getOpStack().size() -1);

	syn ReferenceNode Aaload.getLoadObjectRef(State frame) =
		(ReferenceNode) frame.getOpStack().get(frame.getOpStack().size() -2);		
	
	syn ReferenceNode GetStatic.getLoadObjectRef(State frame) = ReferenceNode.getGlobalRef();
	

	eq FieldLoadInstruction.statesOut() =
		// notation: load obj.field 
		States.processStates(statesIn(), new StateProcessor() {
			@Override public State process(State original) {
				OpStack opStack = original.getOpStack();
				ConnectionGraph cg = original.getCg();
				Slot result;
				
				if ( getFieldType() instanceof org.apache.bcel.generic.ReferenceType ) {
					result = new ReferenceNode(getPosition(), Category.LOCAL);
					Set<ObjectNode> allTargets = new HashSet<ObjectNode>();

					for ( ObjectNode obj :  cg.dereference(getLoadObjectRef(original))) {
//						if ( obj.isGlobal() ) {
//							allTargets.add(obj);
//							continue;
//						}
			
						Set<ObjectNode> targets 
							= cg.getObjectNodes().getFieldOf(obj, cg.getFieldEdges(), getFieldName());
						if ( targets.isEmpty() ) {
							ObjectNode subObject = 
								PhantomObject.newSubPhantom(obj, getFieldName());
							cg = cg.addField(obj, getFieldName(), subObject);
							targets.add(subObject);
						}
						allTargets.addAll(targets);
					}
							
					cg = cg.addReferenceToTargets((ReferenceNode) result, allTargets);
					
				} else 
					result = DontCareSlot.values()[getProduceStack()];					
					
				return new State(
					original.getLocalVars(), 
					opStack.pop(getConsumeStack()).push(result, getProduceStack()), 
					cg);									
			}
		});	

	eq Ldc.statesOut() =
		States.processStates(statesIn(), new StateProcessor() {
			@Override public State process(State original) {			
				return new State(
					original.getLocalVars(), 
					original.getOpStack().push(
						(getConstantType() instanceof org.apache.bcel.generic.ReferenceType ) ? 
						ReferenceNode.getGlobalRef() 
						: DontCareSlot.NORMAL_SLOT), 
					original.getCg());
			}
		});	

			
	eq NewInstruction.statesOut() =
		States.processStates(statesIn(), new StateProcessor() {
			@Override public State process(State original) {
				ConnectionGraph cg = original.getCg();
				
				ObjectNode obj = new InternalObject(
					method().clazz().getName() + "." 
						+ method().getMethodName() + "|"
						+ method().getSignatureIndex() + ":"
						+ getPosition(), 
					getType(), 
					ClassHelper.isRunnable(getType()) ? EscapeState.GLOBAL_ESCAPE : EscapeState.NO_ESCAPE);
					
				ReferenceNode ref = new ReferenceNode(getPosition(), Category.LOCAL);
				cg = cg.addReferenceAndTarget(ref, obj); 
				return new State(
					original.getLocalVars(), 
					original.getOpStack().pop(getConsumeStack()).push(ref), 
					cg); 
			}
		});
	
	eq InvokeInstruction.statesOut() {
		Set<State> result = new HashSet<State>();

		for ( State frameIn : statesIn() ) 
			for ( Method targetMethod : resolveTargetMethod() ) 
				result.add(frameIn.applyMethodSummary(
					targetMethod.methodSummary(), 
					getConsumeStack(), 
					getProduceStack(), 
					getReturnType()));
					
		return result;
	}
		
	eq LoadInstruction.statesOut() =
		States.processStates(statesIn(), new StateProcessor() {
			@Override public State process(State original) {
				return new State(
					original.getLocalVars(), 
					original.getOpStack().push(original.getLocalVars().get(getIndex()), 
						getProduceStack()), 
					original.getCg());
			}
		});
	
	eq StoreInstruction.statesOut() =
		States.processStates(statesIn(), new StateProcessor() {
			@Override public State process(State original) {
				return new State(
					original.getLocalVars().set(getIndex(), 
					original.getOpStack().peek()), original.getOpStack().pop(getConsumeStack()), 
					original.getCg());
			}
		});
	
	eq DUP.statesOut() =
		States.processStates(statesIn(), new StateProcessor() {
			@Override public State process(State original) {
				return new State(
					original.getLocalVars(), 
					original.getOpStack().push(original.getOpStack().peek()), 
					original.getCg());
			}
		});
	
	eq DUP_X1.statesOut() =
		States.processStates(statesIn(), new StateProcessor() {
			@Override public State process(State original) {				
				return new State(
					original.getLocalVars(), 
					original.getOpStack().pop().pop().push(original.getOpStack().peek())
						.push(original.getOpStack().pop().peek()).push(original.getOpStack().peek()), 
					original.getCg());
			}
		});
	
	eq DUP_X2.statesOut() =
		States.processStates(statesIn(), new StateProcessor() {
			@Override public State process(State original) {				
				return new State(
					original.getLocalVars(), 
					original.getOpStack().pop().pop().pop().push(original.getOpStack().peek())
						.push(original.getOpStack().pop().pop().peek())
						.push(original.getOpStack().pop().peek())
						.push(original.getOpStack().peek()), 
					original.getCg());
			}
		});
	
	eq DUP2.statesOut() =
		States.processStates(statesIn(), new StateProcessor() {
			@Override public State process(State original) {
				return new State(
					original.getLocalVars(), 
					original.getOpStack().push(original.getOpStack().pop().peek())
						.push(original.getOpStack().peek()), 
					original.getCg());
			}
		});
	
	eq DUP2_X1.statesOut() =
		States.processStates(statesIn(), new StateProcessor() {
			@Override public State process(State original) {
				return new State(
					original.getLocalVars(), 
					original.getOpStack().pop().pop().pop()
						.push(original.getOpStack().pop().peek())
						.push(original.getOpStack().peek())
						.push(original.getOpStack().pop().pop().peek())
						.push(original.getOpStack().pop().peek())
						.push(original.getOpStack().peek()), 
					original.getCg());
			}
		});
	
	eq DUP2_X2.statesOut() =
		 States.processStates(statesIn(), new StateProcessor() {
			@Override public State process(State original) {
				return new State(
					original.getLocalVars(), 
					original.getOpStack().pop().pop().pop().pop()
						.push(original.getOpStack().pop().peek())
						.push(original.getOpStack().peek())
						.push(original.getOpStack().pop().pop().pop().peek())
						.push(original.getOpStack().pop().pop().peek())
						.push(original.getOpStack().pop().peek())
						.push(original.getOpStack().peek()), 
					original.getCg());
			}
		});
	
	eq SWAP.statesOut() =
		States.processStates(statesIn(), new StateProcessor() {
			@Override public State process(State original) {
				return new State(
					original.getLocalVars(), 
					original.getOpStack().pop().pop()
						.push(original.getOpStack().peek())
						.push(original.getOpStack().pop().peek()), 
					original.getCg());
			}
		});
	
	eq ReturnInstruction.statesOut() = null;
	eq Areturn.statesOut() = null;
		
}